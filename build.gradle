plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

ext {
    springAiVersion = "1.1.0"
}

group = 'kr.or.kosa'
version = '0.0.1-SNAPSHOT'
description = 'backend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    /* ------------------------
     * 기본 Spring Boot 설정
     * ------------------------ */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    /* Validation */
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    /* MyBatis */
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
    implementation 'io.github.cdimascio:dotenv-java:2.2.3'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.projectreactor.netty:reactor-netty'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    /* AI */
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    implementation 'org.springframework.ai:spring-ai-starter-vector-store-qdrant'

    /* DB (MariaDB & MySQL) */
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.3'
    implementation 'com.mysql:mysql-connector-j:8.3.0'

    /* JWT */
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    /* Lombok */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    /* 개발 편의 */
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    /* AWS S3 */
    implementation 'software.amazon.awssdk:s3:2.25.39'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.0.2'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter:3.0.2'

    /* AOP */
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    /* Logstash */
    implementation 'net.logstash.logback:logstash-logback-encoder:7.3'

    /* Jsoup */
    implementation 'org.jsoup:jsoup:1.17.2'

    /* Test */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    /* macOS native resolver (optional) */
    if (isAppleSilicon()) {
        runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64")
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:$springAiVersion"
    }
}

def isAppleSilicon() {
    return System.getProperty("os.name") == "Mac OS X" &&
            System.getProperty("os.arch") == "aarch64"
}

bootJar {
    mainClass = 'kr.or.kosa.backend.BackendApplication'
}

bootRun {
    mainClass = 'kr.or.kosa.backend.BackendApplication'
}

tasks.named('test') {
    useJUnitPlatform()
}